/* ------------------------------------------------------------------------------
 * Copyright (c) 2006 Novell, Inc. All Rights Reserved.
 *
 *
 * This program is free software; you can redistribute it and/or modify it under
 * the terms of version 2 of the GNU General Public License as published by the
 * Free Software Foundation.
 *
 * This program is distributed in the hope that it will be useful, but WITHOUT
 * ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License along with
 * this program; if not, contact Novell, Inc.
 *
 * To contact Novell about this file by physical or electronic mail, you may find
 * current contact information at www.novell.com.
 * ------------------------------------------------------------------------------
 */

/**
 * File:	include/cluster/dialogs.ycp
 * Package:	Configuration of cluster
 * Summary:	Dialogs definitions
 * Authors:	Cong Meng <cmeng@novell.com>
 *
 * $Id: dialogs.ycp 27914 2006-02-13 14:32:08Z locilka $
 */

{

textdomain "cluster";

import "Label";
import "Wizard";
import "Cluster";
import "IP";
import "Popup";
import "Service";
import "Report";
import "CWMFirewallInterfaces";
import "SuSEFirewall";
import "SuSEFirewallServices";

include "cluster/helps.ycp";
include "cluster/common.ycp";

list<string> csync2_suggest_files = ["/etc/corosync/corosync.conf",
	"/etc/corosync/authkey", "/etc/sysconfig/pacemaker",
	"/etc/drbd.d", "/etc/lvm/lvm.conf", "/etc/multipath.conf",
	"/etc/ha.d/ldirectord.cf", "/etc/ctdb/nodes", "/etc/samba/smb.conf",
	"/etc/sysconfig/pacemaker", "/etc/sysconfig/openais",
	"/etc/csync2/csync2.cfg", "/etc/csync2/key_hagroup",
];

string csync2_port = "30865";

// return `cacel or a string
any text_input_dialog(string title, string value)
{
    any ret = nil;

    UI::OpenDialog(
        `MarginBox(1, 1, `VBox(
            `MinWidth(100, `TextEntry(`id(`text), title, value)),
            `VSpacing(1),
            `Right(`HBox(
                `PushButton(`id(`ok), _("OK")),
                `PushButton(`id(`cancel), _("Cancel"))
            ))
        ))
    );

    ret = UI::UserInput();
    if (ret == `ok) ret = UI::QueryWidget(`text, `Value);
    UI::CloseDialog();
    return ret;
}


boolean ValidateCommunication ()
{
	integer i = 0;
	if (IP::Check((string)UI::QueryWidget(`id(`bindnetaddr1), `Value)) == false) {
		Popup::Message("The Bind Network Address has to be fulfilled");
		UI::SetFocus(`bindnetaddr1);
		return false;
	}

	if (UI::QueryWidget(`id(`transport), `Value) == "udpu") {
		i = 0;
		foreach(string value, Cluster::memberaddr1, {
			if (IP::Check(value) == false) {
				UI::ChangeWidget(`memberaddr1, `CurrentItem, i);
				i = 0;
				break;
			}
			i = i + 1;
		});
		if (i == 0) {
			UI::SetFocus(`memberaddr1);
			Popup::Message("The Member Address has to be fulfilled");
			return false;
		}
	} else {
		if (IP::Check((string)UI::QueryWidget(`id(`mcastaddr1), `Value)) == false) {
			Popup::Message("The Multicast Address has to be fulfilled");
			UI::SetFocus(`mcastaddr1);
			return false;
		}
	}

	if (!regexpmatch((string)UI::QueryWidget(`id(`mcastport1), `Value), "^[0-9]+$")) {
		Popup::Message("The Multicast port must be a positive integer");
		UI::SetFocus(`id(`mcastport1));
		return false;
	}

	if (UI::QueryWidget(`id(`autoid), `Value) == false) {
		string noid = (string)UI::QueryWidget(`id(`nodeid), `Value);
		boolean s = regexpmatch(noid, "^[0-9]+$");
		if (!s) {
			Popup::Message("Node ID has to be a positive integer");
			UI::SetFocus(`id(`nodeid));
			return false;
		}
		integer i = tointeger(noid);
		if (i == 0) {
			Popup::Message("NodeID 0 is reserved");
			UI::SetFocus(`id(`nodeid));
			return false;
		}
	}

	if (UI::QueryWidget(`id(`enable2), `Value) == true) {
		if (IP::Check((string)UI::QueryWidget(`id(`bindnetaddr2), `Value)) == false) {
			Popup::Message("The Bind Network Address has to be fulfilled");
			UI::SetFocus(`bindnetaddr2);
			return false;
		}

		if (UI::QueryWidget(`id(`transport), `Value) == "udpu") {
			i = 0;
			foreach(string value, Cluster::memberaddr2, {
				if (IP::Check(value) == false) {
					UI::ChangeWidget(`memberaddr2, `CurrentItem, i);
					i = 0;
					break;
				}
				i = i + 1;
			});
			if (i == 0) {
				UI::SetFocus(`memberaddr2);
				Popup::Message("The Member Address has to be fulfilled");
				return false;
			}
		} else {
			if (IP::Check((string)UI::QueryWidget(`id(`mcastaddr2), `Value)) == false) {
				Popup::Message("The Multicast Address has to be fulfilled");
				UI::SetFocus(`mcastaddr2);
				return false;
			}
		}

		if (!regexpmatch((string)UI::QueryWidget(`id(`mcastport2), `Value), "^[0-9]+$")) {
			Popup::Message("The Multicast port must be a positive integer");
			UI::SetFocus(`id(`mcastport2));
			return false;
		}
	}

	return true;
}

void SaveCommunicationToConf ()
{
	SCR::Write(.openais.totem.interface.interface0.bindnetaddr, (string)UI::QueryWidget(`id(`bindnetaddr1), `Value));
	SCR::Write(.openais.totem.interface.interface0.mcastaddr, (string)UI::QueryWidget(`id(`mcastaddr1), `Value));
	SCR::Write(.openais.totem.interface.interface0.mcastport, (string)UI::QueryWidget(`id(`mcastport1), `Value));

	if (UI::QueryWidget(`id(`enable2), `Value) == false) {
		SCR::Write(.openais.totem.interface.interface1, "");
	} else {
		SCR::Write(.openais.totem.interface.interface1.bindnetaddr, (string)UI::QueryWidget(`id(`bindnetaddr2), `Value));
		SCR::Write(.openais.totem.interface.interface1.mcastaddr, (string)UI::QueryWidget(`id(`mcastaddr2), `Value));
		SCR::Write(.openais.totem.interface.interface1.mcastport, (string)UI::QueryWidget(`id(`mcastport2), `Value));

	}

	if (UI::QueryWidget(`id(`autoid), `Value) == true) {
		SCR::Write(.openais.totem.autoid, "yes");
		SCR::Write(.openais.totem.nodeid, "0");
	} else {
		SCR::Write(.openais.totem.nodeid, (string)UI::QueryWidget(`id(`nodeid), `Value));
		SCR::Write(.openais.totem.autoid, "no");
	}

	SCR::Write(.openais.totem.rrpmode, (string)UI::QueryWidget(`id(`rrpmode), `Value));
}

void SaveCommunication () {
	Cluster::bindnetaddr1 = (string)UI::QueryWidget(`id(`bindnetaddr1), `Value);
	Cluster::bindnetaddr2 = (string)UI::QueryWidget(`id(`bindnetaddr2), `Value);
	Cluster::mcastaddr1   = (string)UI::QueryWidget(`id(`mcastaddr1), `Value);
	Cluster::mcastaddr2   = (string)UI::QueryWidget(`id(`mcastaddr2), `Value);
	Cluster::mcastport1   = (string)UI::QueryWidget(`id(`mcastport1), `Value);
	Cluster::mcastport2   = (string)UI::QueryWidget(`id(`mcastport2), `Value);
	Cluster::enable2      = (boolean)UI::QueryWidget(`id(`enable2), `Value);
	Cluster::autoid       = (boolean)UI::QueryWidget(`id(`autoid), `Value);
	Cluster::nodeid       = (string)UI::QueryWidget(`id(`nodeid), `Value);
	Cluster::rrpmode      = (string)UI::QueryWidget(`id(`rrpmode), `Value);
	Cluster::transport    = (string)UI::QueryWidget(`id(`transport), `Value);
}


string calc_network_addr(string ip, string mask) {
	string str = IP::IPv4ToBits(ip);
	str = substring(str, 0, tointeger(mask));
	while (size(str) < 32) {
		str = str + "0";
	}
	return IP::BitsToIPv4(str);
}


void transport_switch() {
	boolean udp = (string)UI::QueryWidget(`id(`transport), `Value) == "udp";
	boolean enable2 = (boolean)UI::QueryWidget(`id(`enable2), `Value);

	boolean enable1 = udp;
	enable2 = udp && enable2;

	UI::ChangeWidget(`id(`mcastaddr1), `Enabled, enable1);
	UI::ChangeWidget(`id(`memberaddr1), `Enabled, !enable1);
	UI::ChangeWidget(`id(`memberaddr1_add), `Enabled, !enable1);
	UI::ChangeWidget(`id(`memberaddr1_del), `Enabled, !enable1);
	UI::ChangeWidget(`id(`memberaddr1_edit), `Enabled, !enable1);

	UI::ChangeWidget(`id(`mcastaddr2), `Enabled, enable2);
	UI::ChangeWidget(`id(`memberaddr2), `Enabled, !enable2);
	UI::ChangeWidget(`id(`memberaddr2_add), `Enabled, !enable2);
	UI::ChangeWidget(`id(`memberaddr2_del), `Enabled, !enable2);
	UI::ChangeWidget(`id(`memberaddr2_edit), `Enabled, !enable2);
}


any CommunicationLayout() {
	map result = $[];

	result = (map)SCR::Execute(.target.bash_output, "/sbin/ip addr show scope global | grep inet | awk '{print $2}' | awk -F'/' '{print $1, $2}'");

	list <string> existing_ips = [];
	if (size(result["stdout"]:"") != 0) {
		list <string> ip_masks = splitstring(result["stdout"]:"", "\n");
		foreach (string s, ip_masks, {
			list <string> ip_mask_list = splitstring(s, " ");
			string ip = ip_mask_list[0]:"";
			string mask = ip_mask_list[1]:"";
			boolean ip4 = false;
			ip4 = IP::Check4(ip);
			if (ip4) {
				existing_ips = add(existing_ips, calc_network_addr(ip, mask));
			}
		});
	}

	term transport = `ComboBox(`id(`transport), `opt(`hstretch, `notify), _("Transport:"), ["udp", "udpu"]);

	term iface = `Frame( _("Channel"),
		`VBox(
			`ComboBox(`id(`bindnetaddr1), `opt(`editable, `hstretch, `notify), _("Bind Network Address:"), toset(existing_ips)),
			`InputField(`id(`mcastaddr1), `opt(`hstretch, `notify), _("Multicast Address:")),
			`InputField(`id(`mcastport1), `opt(`hstretch), _("Multicast Port:")),
			`Left(`Label(_("Member Address:"))),
			`SelectionBox(`id(`memberaddr1), ""),
			`HBox(
				`PushButton(`id(`memberaddr1_add), "Add"),
				`PushButton(`id(`memberaddr1_del), "Del"),
				`PushButton(`id(`memberaddr1_edit), "Edit")
			)
		)
	);

	term riface = `CheckBoxFrame(`id(`enable2),  `opt(`notify), _("Redundant Channel"), false,
		`VBox(
			`ComboBox(`id(`bindnetaddr2), `opt(`editable, `hstretch, `notify), "Bind Network Address:", existing_ips),
			`InputField(`id(`mcastaddr2), `opt(`hstretch), "Multicast Address:"),
			`InputField(`id(`mcastport2), `opt(`hstretch), "Multicast Port:"),
			`Left(`Label(_("Member Address:"))),
			`SelectionBox(`id(`memberaddr2), ""),
			`HBox(
				`PushButton(`id(`memberaddr2_add), "Add"),
				`PushButton(`id(`memberaddr2_del), "Del"),
				`PushButton(`id(`memberaddr2_edit), "Edit")
			)
		)
	);

	term nid = `VBox(
		`InputField(`id(`nodeid), `opt(`hstretch), "Node ID:"),
		`Left(`CheckBox(`id(`autoid), `opt(`notify), "Auto Generate Node ID", true))
	);

	term rrpm =  `ComboBox(`id(`rrpmode), `opt(`hstretch), "rrp mode:", ["none", "active", "passive"]);

	term contents = `VBox(
		transport,
		`HBox(
			`VBox(iface, nid),
			`VBox(riface, rrpm, `VSpacing(1))
		)
	);

	my_SetContents("communication", contents);

	UI::ChangeWidget(`id(`bindnetaddr1), `Value, Cluster::bindnetaddr1);
	UI::ChangeWidget(`id(`mcastaddr1), `Value, Cluster::mcastaddr1);
	UI::ChangeWidget(`id(`mcastport1), `Value, Cluster::mcastport1);
	UI::ChangeWidget(`id(`enable2), `Value, Cluster::enable2);
	UI::ChangeWidget(`id(`bindnetaddr2), `Value, Cluster::bindnetaddr2);
	UI::ChangeWidget(`id(`mcastaddr2), `Value, Cluster::mcastaddr2);
	UI::ChangeWidget(`id(`mcastport2), `Value, Cluster::mcastport2);

	UI::ChangeWidget(`id(`autoid), `Value, Cluster::autoid);
	UI::ChangeWidget(`id(`nodeid), `Value, Cluster::nodeid);
	UI::ChangeWidget(`id(`transport), `Value, Cluster::transport);

	UI::ChangeWidget(`id(`rrpmode), `Value, Cluster::rrpmode);

	if (UI::QueryWidget(`id(`autoid), `Value) == true) {
		UI::ChangeWidget(`id(`nodeid), `Enabled, false);
	}

	transport_switch();
}


void fill_memberaddr_entries()
{
	integer i = 0;
	integer ret = 0;
	integer current = 0;
	list<term> items = [];

	// remove duplicated elements
	Cluster::memberaddr1 = Cluster::memberaddr1 + [];
	Cluster::memberaddr2 = Cluster::memberaddr2 + [];

	i = 0;
	items = [];
	foreach(string value, Cluster::memberaddr1, {
		items = add(items, `item(`id(i), value));
		i = i + 1;
	});
	current = (integer)UI::QueryWidget(`memberaddr1, `CurrentItem);
	if (current == nil) current = 0;
	if (current >= i) current = i - 1;
	UI::ChangeWidget(`memberaddr1, `Items, items);
	UI::ChangeWidget(`memberaddr1, `CurrentItem, current);

	i = 0;
	items = [];
	foreach(string value, Cluster::memberaddr2, {
		items = add(items, `item(`id(i), value));
		i = i + 1;
	});
	current = (integer)UI::QueryWidget(`memberaddr2, `CurrentItem);
	if (current == nil) current = 0;
	if (current >= i) current = i - 1;
	UI::ChangeWidget(`memberaddr2, `Items, items);
	UI::ChangeWidget(`memberaddr2, `CurrentItem, current);
}

any CommunicationDialog() {
	any ret = nil;

	CommunicationLayout();

	while (true) {
		fill_memberaddr_entries();
		transport_switch();

		ret = UI::UserInput();

		if (ret == `bindnetaddr1 || ret == `bindnetaddr2 || ret == `mcastaddr1 || ret == `mcastaddr2) {
			boolean ip6 = false;
			string netaddr = (string)UI::QueryWidget(`id(ret), `Value);
			ip6 = IP::Check6(netaddr);
			if (ip6) {
				UI::ChangeWidget(`id(`autoid), `Value, false);
				UI::ChangeWidget(`id(`nodeid), `Enabled, true);
				UI::ChangeWidget(`id(`autoid), `Enabled, false);
			} else {
				UI::ChangeWidget(`id(`autoid), `Enabled, true);
			}
			continue;
		}

		if (ret == `autoid) {
			UI::ChangeWidget(`id(`nodeid), `Enabled, (true != UI::QueryWidget(`id(`autoid), `Value)));
			continue;
		}

		if (ret == `enable2) {
			if (true == UI::QueryWidget(`id(`enable2), `Value)) {
				UI::ChangeWidget(`id(`rrpmode), `Value , "passive");
			} else {
				UI::ChangeWidget(`id(`rrpmode), `Value , "none");
			}
		}

		if (ret == `memberaddr1_add) {
			ret = text_input_dialog(_("Enter a member address"), "");
			if (ret == `cancel) continue;
			Cluster::memberaddr1 = add(Cluster::memberaddr1, (string)ret);
		}

		if (ret == `memberaddr1_edit) {
			integer current = 0;
			string str = "";

			current = (integer)UI::QueryWidget(`memberaddr1, `CurrentItem);
			ret = text_input_dialog(_("Edit the member address"), Cluster::memberaddr1[current]:"");
			if (ret == `cancel) continue;
			Cluster::memberaddr1[current] = (string)ret;
		}

		if (ret == `memberaddr1_del) {
			integer current = 0;
			current = (integer)UI::QueryWidget(`memberaddr1, `CurrentItem);
			Cluster::memberaddr1= remove(Cluster::memberaddr1, current);
		}

		if (ret == `memberaddr2_add) {
			ret = text_input_dialog(_("Enter a member address"), "");
			if (ret == `cancel) continue;
			Cluster::memberaddr2 = add(Cluster::memberaddr2, (string)ret);
		}

		if (ret == `memberaddr2_edit) {
			integer current = 0;
			string str = "";

			current = (integer)UI::QueryWidget(`memberaddr2, `CurrentItem);
			ret = text_input_dialog(_("Edit the member address"), Cluster::memberaddr2[current]:"");
			if (ret == `cancel) continue;
			Cluster::memberaddr2[current] = (string)ret;
		}

		if (ret == `memberaddr2_del) {
			integer current = 0;
			current = (integer)UI::QueryWidget(`memberaddr2, `CurrentItem);
			Cluster::memberaddr2= remove(Cluster::memberaddr2, current);
		}

		if (ret == `next || ret == `back) {
			boolean val = ValidateCommunication();
			if (val == true) {
				SaveCommunication();
				break;
			} else {
				ret = nil;
				continue;
			}
		}

		if (ret == `abort || ret == `cancel) {
			if (ReallyAbort())
				return ret;
			else
				continue;
		}

		if (ret == `wizardTree)
			ret = (string)UI::QueryWidget (`id (`wizardTree), `CurrentItem);

		if (contains(DIALOG, (string)ret)) {
			ret = symbolof(toterm(ret));
			boolean val = ValidateCommunication();
			if (val == true) {
				SaveCommunication();
				break;
			} else {
				ret = nil;
				Wizard::SelectTreeItem("communication");
				continue;
			}
		}

	    y2error("unexpected retcode: %1", ret);
	}

	return ret;
}

boolean ValidateSecurity () {
	boolean ret = true;
	if (UI::QueryWidget(`id(`secauth), `Value) == true) {
		string thr = (string)UI::QueryWidget(`id(`threads), `Value);
		boolean s = regexpmatch(thr, "^[0-9]+$");
		if (!s) {
			Popup::Message("Number of threads must be integer");
			UI::SetFocus(`id(`threads));
			ret = false;
		}
		integer i = tointeger(thr);
		if (i == 0) {
			Popup::Message("Number of threads must larger then 0");
			UI::SetFocus(`id(`threads));
			ret = false;
		}
	}
	return ret;
}

void SaveSecurityToConf() {
	if (UI::QueryWidget(`id(`secauth), `Value) == true) {
		SCR::Write(.openais.totem.secauth, "on");
		SCR::Write(.openais.totem.threads, (string)UI::QueryWidget(`id(`threads), `Value));
	} else {
		SCR::Write(.openais.totem.secauth, "off");
		SCR::Write(.openais.totem.threads, "");
	}
}

void SaveSecurity () {
	Cluster::secauth = (boolean)UI::QueryWidget(`id(`secauth), `Value);
	Cluster::threads = (string)UI::QueryWidget(`id(`threads), `Value);
}

any SecurityDialog()
{
	any ret = nil;

	term contents = `VBox(
		`VSpacing(1),
		`CheckBoxFrame(`id(`secauth), `opt(`hstretch, `notify),  _("Enable Security Auth"), true,
			`VBox(
				`InputField(`id(`threads), `opt(`hstretch), "Threads:"),
				`VSpacing(1),
				`Label(_("For newly created cluster, push the button below to generate /etc/corosync/authkey.")),
				`Label(_("To join an existing cluster, please copy /etc/corosync/authkey from other nodes manually.")),
				`PushButton(`id(`genf), `opt(`notify), "Generate Auth Key File")
			)
		),
		`VStretch()
	);

	my_SetContents("security", contents);

	UI::ChangeWidget(`id(`secauth), `Value, Cluster::secauth);

	UI::ChangeWidget(`id(`threads), `Value, Cluster::threads);

	while (true) {
		ret = UI::UserInput();

		if (ret == `genf) {
			map result = $[];
			result = (map)SCR::Execute(.target.bash_output, "/usr/sbin/corosync-keygen");
			if (result["exit"]:-1 != 0) {
				Popup::Message("Failed to create /etc/corosync/authkey");
			} else {
				Popup::Message("Create /etc/corosync/authkey succeeded");
			}
			continue;
		}

		if (ret == `secauth) {
			if (UI::QueryWidget(`id(`secauth), `Value) == true) {
				string thr = (string) UI::QueryWidget(`id(`threads), `Value);
				if (thr == "" || thr == "0") {
					map result = $[];
					any t = 0;
					result = (map)SCR::Execute(.target.bash_output, "grep processor /proc/cpuinfo | wc -l");
					t = tointeger(result["stdout"]:"");
					if (t == nil) {
						t = 0;
					}
					UI::ChangeWidget(`id(`threads), `Value, sformat("%1",t));
				}
				continue;
			}
		}

		if (ret == `next || ret == `back) {
			boolean val = ValidateSecurity();
			if (val == true) {
				SaveSecurity();
				break;
			} else {
				ret = nil;
				continue;
			}
		}

		if (ret == `abort || ret == `cancel) {
			if (ReallyAbort())
				return ret;
			else
				continue;
		}

		if (ret == `wizardTree)
			ret = (string)UI::QueryWidget (`id (`wizardTree), `CurrentItem);

		if (contains(DIALOG, (string)ret)) {
			ret = symbolof(toterm(ret));
			boolean val = ValidateSecurity();
			if (val == true) {
				SaveSecurity();
				break;
			} else {
				ret = nil;
				Wizard::SelectTreeItem("security");
				continue;
			}
		}

	    y2error("unexpected retcode: %1", ret);

	}
    return ret;
}

boolean ValidateService() {
	return true;
}

void SaveServiceToConf() {
	if (UI::QueryWidget(`id(`mgmtd), `Value) == true) {
		SCR::Write(.openais.pacemaker.use_mgmtd, "yes");
	} else {
		SCR::Write(.openais.pacemaker.use_mgmtd, "no");
	}
}

void SaveService () {
	Cluster::use_mgmtd = (boolean)UI::QueryWidget(`id(`mgmtd), `Value);
}

void UpdateServiceStatus() {
	integer ret = 0;
	ret = Service::Status("openais");
	if (ret == 0) {
		UI::ChangeWidget(`id(`status), `Value, _("Running"));
	} else {
		UI::ChangeWidget(`id(`status), `Value, _("Not running"));
	}
	UI::ChangeWidget (`id ("start_now"), `Enabled, ret != 0);
	UI::ChangeWidget (`id ("stop_now"), `Enabled, ret == 0);

	map result = $[];
	result = (map)SCR::Execute(.target.bash_output, "/sbin/chkconfig openais 2>/dev/null | awk '{print $2}'");
	if (find(result["stdout"]:"","off") != -1) {
		UI::ChangeWidget(`id("off"), `Value, true);
		UI::ChangeWidget(`id("on"), `Value, false);
	} else {
		UI::ChangeWidget(`id("on"), `Value, true);
		UI::ChangeWidget(`id("off"), `Value, false);
	}
}

any ServiceDialog () {
	any ret = nil;

	/*
	map<string, any> firewall_widget = CWMFirewallInterfaces::CreateOpenFirewallWidget ($[
			"services" : [ "port:5141" ],
			"display_details" : true,
			]);
	term firewall_layout = firewall_widget["custom_widget"]:`VBox();
	*/

	term contents = `VBox(
		`VSpacing(1),

		`Frame(_("Booting"),
			`RadioButtonGroup(`id("bootopenais"),
				`HBox(`HSpacing(1), `VBox (
					`Left(`RadioButton(`id ("on"),  `opt(`notify), _("On -- Start openais at booting"))),
					`Left(`RadioButton(`id ("off"), `opt(`notify), _("Off -- Start openais manually only")))
				))
			)
		),
		`VSpacing(1),
		`Frame (_("Switch On and Off"),
			`Left(`VBox(
				`Left(`HBox(
					`Label(_("Current Status: ")),
					`Label(`id(`status), _("Running")),
					`ReplacePoint(`id ("status_rp"), `Empty())
				)),
				`Left(`HBox(`HSpacing(1), `HBox(
	                `PushButton(`id("start_now" ), _("Start openais Now")),
		            `PushButton(`id("stop_now" ),  _("Stop openais Now"))
				)))
			))
		),
		`VSpacing(1),
		`Frame(_("Management Tool"),
			`Left(`HBox(`HSpacing(1), `CheckBox(`id(`mgmtd), "Enable mgmtd. The GUI client requires this.", true)))
		),
		`VStretch()
	);


	my_SetContents("service", contents);

	UI::ChangeWidget(`id(`mgmtd), `Value, Cluster::use_mgmtd);

	while (true) {
		UpdateServiceStatus();
		ret = UI::UserInput();

		if (ret == "on" || ret == "off") {
			SCR::Execute(.target.bash, sformat("chkconfig openais %1", ret));
			continue;
		}

		if (ret == "start_now") {
			Cluster::save_csync2_conf();
			Cluster::SaveClusterConfig();
			if (!Service::Start("openais")) {
				Report::Error(Service::Error());
			}
			continue;
		}

		if (ret == "stop_now") {
			if (!Service::Stop("openais")) {
				Report::Error(Service::Error());
			}
			continue;
		}

		if (ret == `next || ret == `back) {
			boolean val = ValidateService();
			if (val == true) {
				SaveService();
				break;
			} else {
				ret = nil;
				continue;
			}
		}

		if (ret == `abort || ret == `cancel) {
			if (ReallyAbort())
				return ret;
			else
				continue;
		}

		if (ret == `wizardTree)
			ret = (string)UI::QueryWidget (`id (`wizardTree), `CurrentItem);

		if (contains(DIALOG, (string)ret)) {
			ret = symbolof(toterm(ret));
                        boolean val = ValidateService();
			if (val == true) {
				SaveService();
				break;
			} else {
				ret = nil;
				Wizard::SelectTreeItem("service");
				continue;
			}
		}

	    y2error("unexpected retcode: %1", ret);

	}
    return ret;
}


term csync2_layout()
{
	return
	`VBox(`opt(`hvstretch),
		`HBox(
			`Frame(_("Sync Host"),
			`VBox(
				`SelectionBox(`id(`host_box), ""),
				`HBox(
					`PushButton(`id(`host_add), _("Add")),
					`PushButton(`id(`host_del), _("Del")),
					`PushButton(`id(`host_edit), _("Edit"))
				)
			)),
			`HSpacing(),
			`Frame(_("Sync File"),
			`VBox(
				`SelectionBox(`id(`include_box), ""),
				`HBox(
					`PushButton(`id(`include_add), _("Add")),
					`PushButton(`id(`include_del), _("Del")),
					`PushButton(`id(`include_edit), _("Edit")),
					`PushButton(`id(`include_suggest), _("Add Suggested Files"))
				)
			))
		),
		`HBox(
			`PushButton(`id(`generate_key), `opt(`hstretch), _("Generate Pre-Shared-Keys")),
			`PushButton(`id(`csync2_switch), `opt(`hstretch), "")
		)
	);
}


// return 1 if csync2 is not installed well
// return 2 if csync2 is OFF
// return 3 if csync2 is ON
integer csync2_status()
{
	map ret = nil;

	ret = (map)SCR::Execute(.target.bash_output, "/sbin/chkconfig csync2");
	y2milestone("chkconfig csync2 = %1", ret)
	if (issubstring(ret["stderr"]:"", "command not found") == true) return 1;
	if (issubstring(ret["stderr"]:"", "unknown service") == true) return 1;
	if (issubstring(ret["stdout"]:"", "off") == true) return 2;

	return 3;
}

integer try_restart_xinetd()
{
	integer r = Service::RunInitScript("xinetd", "try-restart");
	y2debug("try_restart_xinetd return %1", r);
	return r;
}

void csync2_turn_off()
{
	SCR::Execute(.target.bash_output, "/sbin/chkconfig csync2 off");
	if (SuSEFirewall::HaveService(csync2_port, "TCP", "EXT"))
		SuSEFirewall::RemoveService(csync2_port, "TCP", "EXT");
	try_restart_xinetd();
}

void csync2_turn_on()
{
	SCR::Execute(.target.bash_output, "/sbin/chkconfig csync2 on");
	if (!SuSEFirewall::HaveService(csync2_port, "TCP", "EXT"))
		SuSEFirewall::AddService(csync2_port, "TCP", "EXT");
	try_restart_xinetd();
}

void fill_csync_entries()
{
	integer i = 0;
	integer ret = 0;
	integer current = 0;
	list<term> items = [];

	// remove duplicated elements
	Cluster::csync2_host = Cluster::csync2_host + [];
	Cluster::csync2_include = Cluster::csync2_include + [];

	i = 0;
	items = [];
	foreach(string value, Cluster::csync2_host, {
		items = add(items, `item(`id(i), value));
		i = i + 1;
	});
	current = (integer)UI::QueryWidget(`host_box, `CurrentItem);
	if (current == nil) current = 0;
	if (current >= i) current = i - 1;
	UI::ChangeWidget(`host_box, `Items, items);
	UI::ChangeWidget(`host_box, `CurrentItem, current);

	i = 0;
	items = [];
	foreach(string value, Cluster::csync2_include, {
		items = add(items, `item(`id(i), value));
		i = i + 1;
	});
	current = (integer)UI::QueryWidget(`include_box, `CurrentItem);
	if (current == nil) current = 0;
	if (current >= i) current = i - 1;
	UI::ChangeWidget(`include_box, `Items, items);
	UI::ChangeWidget(`include_box, `CurrentItem, current);

	ret = csync2_status();
	UI::ChangeWidget(`id(`csync2_switch), `Enabled, (ret != 1));
	if (ret == 1)
		UI::ChangeWidget(`id(`csync2_switch), `Label, _("Csync2 Status Unknown"));
	if (ret == 2)
		UI::ChangeWidget(`id(`csync2_switch), `Label, _("Turn csync2 ON"));
	if (ret == 3)
		UI::ChangeWidget(`id(`csync2_switch), `Label, _("Turn csync2 OFF"));
}


any Csync2Dialog()
{
	any ret = nil;

	my_SetContents("csync2", csync2_layout());


	while(true) {
		fill_csync_entries();

		ret = UI::UserInput();

		if (ret == `abort || ret == `cancel) {
			if (ReallyAbort()) break;
			continue;
		}

		if (ret == `next || ret == `back) {
			break;
		}

		if (ret == `wizardTree)
			ret = (string)UI::QueryWidget(`id(`wizardTree), `CurrentItem);

		if (contains(DIALOG, (string)ret)) {
			ret = symbolof(toterm(ret));
			//SaveCsync2();
			break;
		} else {
			Wizard::SelectTreeItem("csync2");
			continue;
		}

		if (ret == `host_add) {
			ret = text_input_dialog(_("Enter a hostname"), "");
			if (ret == `cancel) continue;
			Cluster::csync2_host = add(Cluster::csync2_host, (string)ret);
		}

		if (ret == `host_edit) {
			integer current = 0;
			string str = "";

			current = (integer)UI::QueryWidget(`host_box, `CurrentItem);
			ret = text_input_dialog(_("Edit the hostname"), Cluster::csync2_host[current]:"");
			if (ret == `cancel) continue;
			Cluster::csync2_host[current] = (string)ret;
		}

		if (ret == `host_del) {
			integer current = 0;
			current = (integer)UI::QueryWidget(`host_box, `CurrentItem);
			Cluster::csync2_host = remove(Cluster::csync2_host, current);
		}

		if (ret == `include_add) {
			ret = text_input_dialog(_("Enter a filename to synchronize"), "");
			if (ret == `cancel) continue;
			Cluster::csync2_include = add(Cluster::csync2_include, (string)ret);
		}

		if (ret == `include_edit) {
			integer current = 0;

			current = (integer)UI::QueryWidget(`include_box, `CurrentItem);
			ret = text_input_dialog(_("Edit the filename"), Cluster::csync2_include[current]:"");
			if (ret == `cancel) continue;
			Cluster::csync2_include[current] = (string)ret;
		}

		if (ret == `include_del) {
			integer current = 0;
			current = (integer)UI::QueryWidget(`include_box, `CurrentItem);
			Cluster::csync2_include = remove(Cluster::csync2_include, current);
		}

		if (ret == `include_suggest) {
			Cluster::csync2_include = Cluster::csync2_include + csync2_suggest_files;
		}

		if (ret == `generate_key) {
			string key_file = Cluster::csync2_key_file;

			// key file exist
			if (SCR::Read(.target.size, key_file) > 0) {
				if (!Popup::YesNo(sformat(_("Key file %1 already exist.\nDo you want to overwrite it?"), key_file)))
					continue;

				// remove exist key file
				if (SCR::Execute(.target.remove, key_file) == false) {
					Popup::Message(sformat(_("Delete key file %1 failed."), key_file));
					continue;
				}
			}

			// generate key file
			ret = SCR::Execute(.target.bash, sformat("csync2 -k %1", key_file));
			if (ret == 0)
				Popup::Message(sformat(_("Key file %1 is generated.\nClicking \"Add Suggested Files\" button adds it to sync list."), key_file));
			else
				Popup::Message(_("Key generation failed."));
		}

		if (ret == `csync2_switch) {
			string label = "";
			label = (string)UI::QueryWidget(`csync2_switch, `Label);
			if (issubstring(label, "OFF")) csync2_turn_off();
			if (issubstring(label, "ON")) csync2_turn_on();
		}
	}

	return ret;
}
/* EOF */
}

